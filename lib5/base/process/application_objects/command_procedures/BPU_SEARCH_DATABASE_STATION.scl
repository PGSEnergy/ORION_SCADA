;Function: BPU_SEARCH_DATABASE_STATION:C
;Source file: sc/lib5/base/process/application_objects/command_procedures/bpu_search_database_station.scl
;Version: 1.0
;Parameters: l_object_info (contents of CD-attribute), U_OTHER_OBJECTS, result
;Return data:
;Description: returns Station L/R index and Station Blockings index
;--------------------------------------------------------------------------------------------------
;Revision History
;Date       Vers.    Author Description
;--------------------------------------------------------------------------------------------------
#argument t_OI, hard_coded_info, t_Obj_ID
#local l_Result = argument(4)
#local l_objects, i, sta_ln = ""
#local i_Count = length(l_result.EVENT_OBJECTS)
#local rx1, rx2

;****************************************************************************************************
;redirect command if application specific command procedure is found                               ;*
#local t_Redirect_Proc = "APLMOD5_BPU_SEARCH_DATABASE_STATION"                                     ;*
#local i_ArgCnt, t_Redirect_Command, i_arg_loop                                                    ;*
#if application_object_exists(0, "C", "'t_Redirect_Proc'") #then #block                            ;*
   #if data_type(%REDIRECTED)=="NONE" #then #block                                                 ;*
      #if 't_Redirect_Proc':CIU == 1 #then #block                                                  ;*
         @REDIRECTED = true                                                                        ;*
         i_ArgCnt = argument_count                                                                 ;*
         #if sys:bdm == 1 #then t_Redirect_Command = "do('t_Redirect_Proc':C"                        ;*
         #else_if 't_Redirect_Proc':ccs == 1 #then t_Redirect_Command = "do('t_Redirect_Proc':CCP" ;*
         #else t_Redirect_Command = "do('t_Redirect_Proc':C"                                       ;*
         #loop_with i_arg_loop = 1 .. i_ArgCnt                                                     ;*
            t_Redirect_Command = t_Redirect_Command + ",argument('i_arg_loop')"                    ;*
            #if i_arg_loop == i_ArgCnt #then t_Redirect_Command =  t_Redirect_Command + ")"        ;*
         #loop_end                                                                                 ;*
         #return 't_Redirect_Command' ;;;;;                                                        ;*
      #block_end                                                                                   ;*
   #block_end                                                                                      ;*
   #else #delete REDIRECTED:V                                                                      ;*
#block_end                                                                                         ;*
;****************************************************************************************************
;MAIN START

#if attribute_exists(hard_coded_info, "station_ln") #then sta_ln = hard_coded_info.station_ln

#if data_type(sta_ln) == "TEXT" #then #block
   #if sta_ln=="NONE" #then l_result.all_objects.sta_lr_required = false
#block_end
#else_if data_type(sta_ln) == "VECTOR" and length(sta_ln)==1 #then #block
   #if data_type(sta_ln(1)) == "TEXT" #then #block
      #if sta_ln(1) =="NONE" #then l_result.all_objects.sta_lr_required = false
      #else sta_ln = sta_ln(1)
   #block_end
#block_end

#if l_result.all_objects.sta_lr_required #then #block

   ;search objects by checking the complete process object database   
   rx1 = "%%%%%%%%%%%%%%%%%%%%%%'t_Obj_ID'ILRS*"
   rx2 = "%%%%%%%%%%%%%%%%%%%%%%'t_Obj_ID'XBLK*"
   #if sta_ln == "" #then l_objects = application_object_list(0, "IX",  "OI", "", "'t_OI'*", "RX==""'RX1'"" OR RX==""'RX2'""", vector("IU", "SS", "RX"))
   #else l_objects = application_object_list(0, "IX", "I", "F", sta_ln, "RX==""'RX1'"" OR RX==""'RX2'""", vector("IU", "SS", "RX"))
   
   #loop_with i = 1 .. l_objects.count
      #if substr(l_objects.rx(i),23,8)=="'t_Obj_ID'ILRS" #then #block
         #if l_objects.iu(i) > 0 and l_objects.ss(i) > 0 #then #block
            l_result.ALL_OBJECTS = merge_attributes(l_result.ALL_OBJECTS, list(Sta_lr_object_ln = l_objects.ln(i), sta_lr_object_ix = l_objects.ix(i) , sta_lr_object = true))
            i_Count = i_Count + 1
            l_result.EVENT_OBJECTS(i_Count) = list(LN = l_objects.ln(i), IX = l_objects.ix(i))
         #block_end
         #else l_result.all_objects.sta_lr_required = false
      #block_end
      ;blocking printout
      #else_if substr(l_objects.rx(i),23,8)=="'t_Obj_ID'XBLK" #then #block
         #if l_objects.iu(i) > 0 and l_objects.ss(i) > 0 #then #block
            l_result.ALL_OBJECTS = merge_attributes(l_result.ALL_OBJECTS, list(blockings_ln = l_objects.ln(i), blockings_ix = l_objects.ix(i)))
            i_Count = i_Count + 1
            l_result.EVENT_OBJECTS(i_Count) = list(LN = l_objects.ln(i), IX = l_objects.ix(i))
         #block_end
      #block_end
   #loop_end      

#block_end

#return l_result
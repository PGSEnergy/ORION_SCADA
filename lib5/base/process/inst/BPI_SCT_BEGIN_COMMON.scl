;Read OI-definitions from APL:BOI
#ARGUMENT T_LEVEL
@L_OI_DEF = APL:BOI
@I_ID_TOT_COUNT = 1
@I_NR_OF_SPECIAL_FIELD = 0

#LOOP_WITH I = 1 .. %L_OI_DEF.DEPTH
	
	#CASE %L_OI_DEF.LEVELS(%I).NAME
		#WHEN "STA" @OBJECT_IDENTIFIER_'I'_NAME = "STATION_NAME"
		#WHEN "BAY" @OBJECT_IDENTIFIER_'I'_NAME = "BAY_NAME"
		#WHEN "DEV" @OBJECT_IDENTIFIER_'I'_NAME = "DEVICE_NAME"
		#OTHERWISE #BLOCK
			@I_NR_OF_SPECIAL_FIELD = %I_NR_OF_SPECIAL_FIELD + 1
			@OBJECT_IDENTIFIER_'I'_NAME = "SPECIAL_IDENTIFIER_'I_NR_OF_SPECIAL_FIELD'"
		#BLOCK_END
	#CASE_END
	
	;create a list of previously used identifiers
   @SECTION  = %OBJECT_IDENTIFIER_'I'_NAME
   @KEYS     = VECTOR()
   #LOOP_WITH J = 1 .. 10
		@NEW_KEY= "'SECTION''J'"
      @KEY'J' = READ_PARAMETER("PICT/OBJ_IDEN.INI",%SECTION,%NEW_KEY)
      @KEY    = %KEY'J'
      #IF KEY:VSTATUS == 0 #THEN #BLOCK
			@KEYS = APPEND(%KEYS,KEY:VVALUE)
      #BLOCK_END
    #LOOP_END
    @OBJECT_IDENTIFIER_'I'_VALUES = %KEYS
	
	@OBJECT_IDENTIFIERS(%I_ID_TOT_COUNT) = LIST(-
		NAME			= %OBJECT_IDENTIFIER_'I'_NAME,-
      TYPE       	= "EDITABLE_SELECTOR",-
      INPUT_TYPE 	= "TEXT",-
      VALUES     	= %OBJECT_IDENTIFIER_'I'_VALUES,-
		REPRESENT  	= %OBJECT_IDENTIFIER_'I'_VALUES,-
      MAX_LENGTH 	= %L_OI_DEF.LEVELS(%I).WIDTH -1)
		
	@I_ID_TOT_COUNT = %I_ID_TOT_COUNT + 1
	
	#IF T_LEVEL == %L_OI_DEF.LEVELS(%I).NAME #THEN #LOOP_EXIT

#LOOP_END

;create a list of all existing authorization groups
#ERROR IGNORE
@AUTHORIZATION_GROUPS = LIST_ATTR(MON:BSV(9))
#IF DATA_TYPE(%AUTHORIZATION_GROUPS) <> "VECTOR" #THEN @AUTHORIZATION_GROUPS = VECTOR()
#ERROR CONTINUE
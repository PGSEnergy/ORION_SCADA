; Function:    BMU_WINTER:C
; Version:     3.0 (Build 20.03.2012)
; Parameters:  none
; Return data: none
; Description: Going to winter time, - 1 hour
;-------------------------------------------------------------------------------
;Revision History
;Date       Vers.    Author Description
;-------------------------------------------------------------------------------
#local i, i_Status = status, cot
#error ignore

#if BMU_GLOBAL:CCS==1 #then #do BMU_GLOBAL:CCP
#else #do BMU_GLOBAL:C

#loop_with i = 1..length(%v_ReportObjectNames)
   @t_ObjectName   = %v_ReportObjectNames(i)
   @l_ReportObject = %l_ReportObjectS.OBJ_'t_ObjectName'

   #if %l_ReportObject.SP_HL>0 #then #block                                     ;*** all except for MP,CP with base period only        

      @T1 = PACK_TIME(YEAR, MONTH, DAY, HOUR, 0, 0)
      @T2 = %T1 + 3600 - 60
      #if BMU_SP_IND:CCS==1 #then #do BMU_SP_IND:CCP
      #else #do BMU_SP_IND:C
   
      @IC1 = %IS1                                                               ;*** indexes of current hour already in Winter time
      @IC2 = %IS2                                                               ;*** indexes of current hour
   
      @T1 = PACK_TIME(YEAR,MONTH,DAY,HOUR,0,0)+3600
      @T2 = %T1+3600-60

      #if BMU_SP_IND:CCS==1 #then #do BMU_SP_IND:CCP
      #else #do BMU_SP_IND:C

      #set 't_ObjectName':DOV(%IC1..%IC2) = 't_ObjectName':DOV(%IS1..%IS2)
      #set 't_ObjectName':DRT(%IC1..%IC2) = 't_ObjectName':DRT(%IS1..%IS2)-3600

      #set 't_ObjectName':DOS(%IS1..%IS2) = 10                                  ;*** set missing hour data to not sampled
      #set 't_ObjectName':DRT(%IS1..%IS2) = %T1
   #block_end

   #if %l_ReportObject.BP_HL>0 #then #block                                     ;*** all except for MS,CS with sampling period only        
      @T1 = PACK_TIME(YEAR, MONTH, DAY, HOUR, 1, 0)
      @T2 = %T1 + 3600 - 120

      #if BMU_BP_IND:CCS==1 #then #do BMU_BP_IND:CCP
      #else #do BMU_BP_IND:C
      
      #case %l_ReportObject.BP
         #when 600 #block                                                       ;*** 10 minute base period 6 values to copy
            #set 't_ObjectName''YE1':DOV(52795..52800) = 't_ObjectName''YE1':D(%IP1..%IP2)
            #set 't_ObjectName''YE1':DRT(52795..52800) = %T1
         #block_end
         #when 900 #block                                                       ;*** 15 minute base period 4 values to copy
            #set 't_ObjectName''YE1':DOV(35147..35150) = 't_ObjectName''YE1':D(%IP1..%IP2)
            #set 't_ObjectName''YE1':DRT(35147..35150) = %T1
         #block_end
         #when 1800 #block                                                      ;*** 30 minutes base period 2 values to copy
            #set 't_ObjectName''YE1':DOV(17599..17600) = 't_ObjectName''YE1':D(%IP1..%IP2)
            #set 't_ObjectName''YE1':DRT(17599..17600) = %T1
         #block_end
         #when 3600 #block                                                      ;*** 1 hour base period 1 value to copy
            #set 't_ObjectName''YE1':DOV8800 = 't_ObjectName''YE1':D(%IP1..%IP1)
            #set 't_ObjectName''YE1':DRT8800 = %T1
         #block_end
      #case_end
   #block_end
#loop_end

i_Status = status
#if i_Status <> 0 #then cot = console_output("BMU_WINTER:C - SCIL Error='i_Status'","E","MREP") 

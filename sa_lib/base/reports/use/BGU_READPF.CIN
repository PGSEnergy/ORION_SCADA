;CM =General Data Interface for Report Applications, Reads the records of period values for given data object
; Function: BGU_READPF:C
; Version: 1.2                                                                  version number in LIB510, used to prevent automatic update
; Parameters: DATA_ID - logical name of the data object
;             T1 - start time
;             T2 - end time (not obl.)
; Return data: DATA:VOV - object data
;                    OS - status codes of data
;                    RT - registration times of data
; Description: Reads the records of period values for given data object
;              (General Data Interface for Report Applications)
;-----------------------------------------------------------------------
#local cot
#ERROR CONTINUE

;read global variables
#if BMU_GLOBAL:CCS==1 #then #DO BMU_GLOBAL:CCP
#else #DO BMU_GLOBAL:C

#IF %T1 > CLOCK-(%PF_Hist_Len-1)*366*86400 #THEN #BLOCK
   #create l_ReportObject:v=list(SP = %SF_Interval, SP_HL = %SF_Hist_Len,-        ;### RSC-HoDa needed for new index calculation methods
                                 BP = %PF_Interval, BP_HL = %PF_Hist_Len)
   ;index calculation
   #if BMU_BP_IND:CCS==1 #then #DO BMU_BP_IND:CCP
   #else #DO BMU_BP_IND:C
   
   #IF %YE1 == %YE2 #THEN #BLOCK
      @DATALOG = %DATA_ID+"'YE1'"
      @DATA = DATA_FETCH(0,%DATALOG,%IP1,1,%NP)
   #BLOCK_END
   #ELSE #BLOCK
      @DATALOG = %DATA_ID+"'YE1'"
      @DF1 = DATA_FETCH(0,%DATALOG,%IP1,1,%NP1)
      @DATALOG = %DATA_ID+"'YE2'"
      @DF2 = DATA_FETCH(0,%DATALOG,1,1,%NP2)
      @DATA = LIST(-
         OV = APPEND(DF1:VOV,DF2:VOV),RT=APPEND(DF1:VRT,DF2:VRT), -
         OS = APPEND(DF1:VOS,DF2:VOS),IX=APPEND(DF1:VIX,DF2:VIX), -
         LE = DF1:VLE+DF2:VLE)
   #BLOCK_END
#BLOCK_END
#ELSE #BLOCK
   @DATA
   cot = console_output("BGU_READPF:C - Time given is out of range for 'DATA_ID'.","W","MREP")      
#BLOCK_END

; Function:    BMU_BP_CLEAN:C
; Version:     3.0 (Build 23.02.2012)
; Parameters:  GLOBAL_VAR
;              v_ObjectNames - all names included in cleaning procedure 
; Return data: none
; Description: Reset 'old' period values 
;-------------------------------------------------------------------------------
;Revision History
;Date       Vers.    Author Description
;-------------------------------------------------------------------------------
#local i, i_Status = status, cot,-
       t_ObjectName = ""
#error ignore

#if BMU_GLOBAL:CCS==1 #then #do BMU_GLOBAL:CCP
#else #DO BMU_GLOBAL:C

#loop_with i = 1..length(%v_ObjectNames)
   t_ObjectName = %v_ObjectNames(i)
   @l_ReportObject = %l_ReportObjects.OBJ_'t_ObjectName'                        ;*** Define time area to reset (reset area 3 days)
   @T1 = max(clock + 86400, clock + (%l_ReportObject.FP - 1) * 86400)           ;*** Act.time + 1 day or if forecast used act.time +forecast days -1
   @T1 = pack_time(year(%T1),month(%T1),day(%T1),0,1,0)                         ;*** Start time one minute after calc. start time
   @T2 = %T1 + %BP_Buffer * 86400 - 120                                         ;*** End time = start time + buffer (3 days)
   
   #if BMU_BP_IND:CCS==1 #then #do BMU_BP_IND:CCP
   #else #do BMU_BP_IND:C
   
   #IF %YE1 == %YE2 #THEN #BLOCK
      #if application_object_exists(0,"D","'t_ObjectName''YE1'") #then #block
         #set 't_ObjectName''YE1':DOS(%IP1..%IP2) = 10
         #set 't_ObjectName''YE1':DRT(%IP1..%IP2) = clock
      #block_end
      #else -
         cot = console_output("BMU_BP_CLEAN:C - Data Object 't_ObjectName''YE1':D missing","E","MREP")      
   #block_end
   #else #block
      #if application_object_exists(0,"D","'t_ObjectName''YE1'") #then #block
         #set 't_ObjectName''YE1':DOS(%IP1..) = 10
         #set 't_ObjectName''YE1':DRT(%IP1..) = clock
      #block_end
      #else -
         cot = console_output("BMU_BP_CLEAN:C - Data Object 't_ObjectName''YE1':D missing","E","MREP")      
      #if application_object_exists(0,"D","'t_ObjectName''YE2'") #then #block
         #set 't_ObjectName''YE2':DOS(1..%IP2) = 10
         #set 't_ObjectName''YE2':DRT(1..%IP2) = clock
      #block_end
      #else -
         cot = console_output("BMU_BP_CLEAN:C - Data Object 't_ObjectName''YE2':D missing","E","MREP")      
   #block_end
#loop_end

;*** Show notification, if errors...
i_Status = status
#if i_Status <> 0 #then -
   cot = console_output("BMU_BP_CLEAN:C - SCIL Error='i_Status'","E","MREP") 




; Function:   BMU_SP_CAL2:C
; Version:    3.0 (Build 10.02.2012)
; Parameters: Object_Names - vector of object names for calculation
;             
; Return data: none
; Description: Update sampled values (Part 2)  CS,CB
;-------------------------------------------------------------------------------
;Revision History
;Date       Vers. Author Description
;-------------------------------------------------------------------------------
#local i, i_Status = status, cot,-
       v_ObjNamesCalc = vector(),-
       l_Now=list(), l_Previous=list(),-
	   b_SummertimeSwitch = false, b_WintertimeSwitch = false
#error ignore

#if BMU_GLOBAL:CCS==1 #then #DO BMU_GLOBAL:CCP
#else #DO BMU_GLOBAL:C

l_Now      = local_time
l_Previous = local_time_add(l_Now, -60 - %SP_DELAY)                             ;*** check if previous sampling executed in WT
#if l_Now.DS and not l_Previous.DS #then b_SummertimeSwitch = true              ;*** now we have ST, previous was in WT
#if not l_Now.DS and l_Previous.DS #then b_WintertimeSwitch = true              ;*** now we have WT, previous was in ST

v_ObjNamesCalc = substr(%l_Calculation.Order_SP,5,0)
#loop_with i = 1..length(v_ObjNamesCalc)                                        ;*** this to keep the order as defined in Report Object Configuration tool
   @t_ObjectName = v_ObjNamesCalc(i)                                            ;*** declared as GLOBAL because needed in BMU_SP_SUM:C

   #if find_element(%v_ObjectNames,%t_ObjectName) > 0 #then #block              ;*** check if this object neds to be handled in this sampling period
      @l_ReportObject = %l_ReportObjects.OBJ_'t_ObjectName'                     ;*** contains all information we need
   
      @T1 = clock - (%l_ReportObject.SP + %SP_Delay)
      #if b_SummertimeSwitch #then @T1 = %T1 - 3600
      #if b_WintertimeSwitch #then @T1 = %T1 + 3600
   
      #if BMU_SP_IND:CCS==1 #then #DO BMU_SP_IND:CCP
      #else #DO BMU_SP_IND:C
   
      #if attribute_exists(%l_ReportObject,"CalcData") #then #block
         #DO %l_ReportObject.CalcData                                           ;*** MUST contaion DATAIN and OPER var. declaration
         #if BMU_SP_SUM:CCS==1 #then #DO BMU_SP_SUM:CCP
         #else #DO BMU_SP_SUM:C
      #block_end
      #else -
         cot = console_output("BMU_SP_CAL2:C - Report object 't_ObjectName' - Calculation operation is missing.","E","MREP")      
   #block_end
#loop_end

#exec BMU_SP_CLBK:C                                                             ;*** execute the callback method for calculated objects

i_Status = status
#if i_Status <> 0 #then cot = console_output("BMU_SP_CAL2:C - SCIL Error='i_Status'","E","MREP") 

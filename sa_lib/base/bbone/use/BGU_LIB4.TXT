;LIB4 backward compatibility, this file is executed by bgu_sysapl.txt at application start-up


#IF DIRECTORY_MANAGER("EXISTS", FM_SCIL_DIRECTORY("/LIB4/BASE/BBONE/USE")) #THEN #BLOCK

   ;Create PATH4.TXT if LIB4 is installed
   #PATH LIB4 +NO_CREATE,/LIB4/BASE/BBONE/USE
   #IF FILE_MANAGER("EXISTS",  FM_SCIL_FILE("LIB4/BGU_GLPATH.TXT")) #THEN #DO READ_TEXT("LIB4/BGU_GLPATH.TXT")
   #IF FILE_MANAGER("EXISTS",  FM_SCIL_FILE("LIB4/BGU_PATHCR.TXT")) #THEN #DO READ_TEXT("LIB4/BGU_PATHCR.TXT")


   ;Add missing items to LIB4/Tools.ini from SA_LIB
   #IF FILE_MANAGER("EXISTS",  FM_SCIL_FILE("LIB4/TOOLS.INI")) #THEN #BLOCK      

      @TOOL_FILES = ("\SC\SA_LIB\SERVICES\TOOLSTARTER\TOOLS.INI", "\SC\STOOL\MISC\TOOLS.INI")
      @LIB4_FILE = "LIB4/TOOLS.INI"

      #LOOP_WITH J = 1 .. LENGTH(%TOOL_FILES)
      
         @FILE = %TOOL_FILES(%J)
         @V_LINES1= READ_TEXT("'FILE'")
         @V_LINE_NUMBERS1 = SELECT(LOCATE( %V_LINES1, "["), ">0")
         @V_SECTIONS1 = PICK(%V_LINES1, %V_LINE_NUMBERS1)
   
         @V_LINES2= READ_TEXT("'LIB4_FILE'")
         @V_SECTIONS2 = PICK(%V_LINES2, SELECT(LOCATE( %V_LINES2, "["), ">0"))
   
         #LOOP_WITH I = 1 .. LENGTH(%V_SECTIONS1)
   
         @TMP_SECTION = %V_SECTIONS1(%I)
         #IF FIND_ELEMENT(%V_SECTIONS2, %TMP_SECTION) == 0 #THEN #BLOCK
        
            #IF %I < LENGTH(%V_LINE_NUMBERS1) #THEN -
               @V_LINES2 = APPEND(%V_LINES2, %V_LINES1(%V_LINE_NUMBERS1(%I) .. %V_LINE_NUMBERS1(%I + 1) - 1))
            #ELSE -
               @V_LINES2 = APPEND(%V_LINES2, %V_LINES1(%V_LINE_NUMBERS1(%I) .. LENGTH(%V_LINES1)))
          
         #BLOCK_END
   
         
         #LOOP_END
         
         @I_RES = WRITE_TEXT(%LIB4_FILE, %V_LINES2)
         
      #LOOP_END

   #BLOCK_END
   
   ;replace LIB4 project manager with SA_LIB project manager
   @T_PRJMGR_LIB4 = FM_SCIL_FILE("LIB4/PRJMGR.VSO")
   @T_PRJMGR_SA_LIB = FM_FILE("\SC\SA_LIB\SERVICES\TOOLSTARTER\PRJMGR.VSO")
   @T_PRJMGR_LIB4_RENAMED = FM_SCIL_FILE("LIB4/PRJMGR_RENAMED_BY_SA_LIB.VSO")
   #IF FILE_MANAGER("EXISTS",  FM_SCIL_FILE("LIB4/PRJMGR.VSO")) #THEN #BLOCK
      @FILE_ATTR_LIB4 = FILE_MANAGER("GET_ATTRIBUTES", %T_PRJMGR_LIB4)
      @FILE_ATTR_SA_LIB = FILE_MANAGER("GET_ATTRIBUTES", %T_PRJMGR_SA_LIB)
      #IF %FILE_ATTR_LIB4.MODIFIED_S <> %FILE_ATTR_SA_LIB.MODIFIED_S #THEN #BLOCK
         @I_COPY = FILE_MANAGER("COPY",  %T_PRJMGR_LIB4, %T_PRJMGR_LIB4_RENAMED, "OVERWRITE")
         #IF %I_COPY == 0 #THEN #BLOCK
            @I_COPY = FILE_MANAGER("COPY", %T_PRJMGR_SA_LIB, %T_PRJMGR_LIB4, "OVERWRITE")
         #BLOCK_END
      #BLOCK_END
   #BLOCK_END

#BLOCK_END
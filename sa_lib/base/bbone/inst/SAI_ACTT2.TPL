#ERROR CONTINUE

; Defination of colors in Bus bar coloring
; Scope "M", Range 33 - 37
@COLOR_PASSIVE  = 33
@COLOR_GROUNDED = 34
@COLOR_UNKNOWN  = 37

; The value of this variable can be changhed in
; "SAGR_INST/SAI_PROJCT.TXT"
@GROUNDED_PROPAGATE = FALSE

; These lines are changed during installation of 
; Command procedure, with the actual index numbers
@WINDING1 = (250, 241, 246, 242, 247, 251, 253, 254, 240)
@WINDING2 = (251, 242, 247, 241, 246, 250, 254, 253, 240)

@V = SELECT((%WINDING1(3), %WINDING2(3)), "==%IX")
@WINDING_NR = DEC(%V(1),1)

@TMP_COLOR      = %WINDING'WINDING_NR'(1)
@TMP_SWITCH     = %WINDING'WINDING_NR'(2)
@TMP_PASSIVE    = %WINDING'WINDING_NR'(3)
@TMP_GROUND_SW  = %WINDING'WINDING_NR'(7)
@TMP_SWITCH1    = %WINDING'WINDING_NR'(4)
@TMP_PASSIVE1   = %WINDING'WINDING_NR'(5)
@TMP_COLOR1     = %WINDING'WINDING_NR'(6)
@TMP_GROUND_SW1 = %WINDING'WINDING_NR'(8)
@TMP_DUMMY      = %WINDING'WINDING_NR'(9)

; #DO READ_TEXT("BBONE/PATH4.TXT")
#DO READ_TEXT("SAGR_INST/SAI_PROJCT.TXT")

#IF      (%AI == %COLOR_PASSIVE) #THEN #BLOCK
  #SET 'LN':PBI'TMP_SWITCH1'    =  0
  #SET 'LN':PBI'TMP_GROUND_SW1' =  0
  #SET 'LN':PBI'TMP_DUMMY'      =  0
#BLOCK_END    
#ELSE_IF (%AI == %COLOR_GROUNDED) #THEN #BLOCK
  #IF      ('LN':PBI'TMP_GROUND_SW' == 0) AND %GROUNDED_PROPAGATE #THEN #BLOCK
    #SET 'LN':PBI'TMP_DUMMY'      =  0
    #SET 'LN':PBI'TMP_GROUND_SW1' =  1
  #BLOCK_END
  #ELSE_IF ('LN':PBI'TMP_GROUND_SW' == 0) AND (NOT %GROUNDED_PROPAGATE) #THEN #BLOCK
    #SET 'LN':PBI'TMP_DUMMY'      =  0
    #SET 'LN':PBI'TMP_GROUND_SW1' =  0
  #BLOCK_END
#BLOCK_END    
#ELSE_IF (%AI == %COLOR_UNKNOWN) #THEN #BLOCK
  #IF  ('LN':PBI'TMP_DUMMY' == 0) #THEN #BLOCK
    #SET 'LN':PBI'TMP_SWITCH'     =  0
    #SET 'LN':PBI'TMP_SWITCH1'    =  0
    #SET 'LN':PBI'TMP_GROUND_SW'  =  0
    #SET 'LN':PBI'TMP_GROUND_SW1' =  0
    #SET 'LN':PBI'TMP_DUMMY'      =  1
  #BLOCK_END
#BLOCK_END    
#ELSE #BLOCK
  #IF      ('LN':PBI'TMP_SWITCH' == 0) #THEN #BLOCK
    #SET 'LN':PBI'TMP_DUMMY'      =  0
    #SET 'LN':PBI'TMP_SWITCH1'    =  1
    #SET 'LN':PBI'TMP_GROUND_SW1' =  0
  #BLOCK_END
#BLOCK_END

; SHADOW_XMT - SHADOW TRANSMITTER EVENT
;
; INPUT PARAMETERS:
;  %APL    - APPLICATION NUMBER
;  %EVENT  - EVENT NUMBER
;  %AS     - APPLICATION STATE
;  %SS     - SHADOWING STATE
;  %SP     - SHADOWING PHASE
;  %STATUS - LAST STATUS

#local t_EV, t_AS, t_SS, t_SP, t_ST

@RT_START = SYS_TIME
@XMT_STATUS = %STATUS
#DO SHADGLOBAL:C

t_EV = %EVENTS(%EVENT + 1)
t_AS = %ASTATE(%AS + 1)
t_SS = %SSTATE(%SS + 1)
t_SP = %SPHASE(%SP + 1)
t_ST = STATUS_CODE_NAME(%XMT_STATUS)

#IF %EVENT == 1 #THEN #BLOCK
   @RCV_OR_XMT="XMT"
   #DO SHADSSTART:C
#BLOCK_END
#ELSE #BLOCK
   @I=TRUNC(SHAD_TYP:D(%APL))
   @MATRIX=%XMT_MATRIX(%I)
#BLOCK_END

#IF %MATRIX <> "" #THEN #DO SHAD_PROC:C

#IF %HSB_LOG #THEN #BLOCK

   #IF %XMT_STATUS <> 0 #THEN #BLOCK
      @TEXT = ("XMT EVENT = 't_EV', AS = 't_AS', SS = 't_SS', SP = 't_SP'",-
               "STATUS = 'XMT_STATUS', 't_ST'")
   #BLOCK_END
   #ELSE #BLOCK
      @TEXT="XMT EVENT = 't_EV', AS = 't_AS', SS = 't_SS', SP = 't_SP'"
   #BLOCK_END
   
   #EXEC SHADLOG:C (@APL=%APL,@RT=%RT_START,@TEXT=%TEXT)

#BLOCK_END         ; IF %HSB_LOG

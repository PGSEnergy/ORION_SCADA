; SHADRSTACH

#DO SHADGLOBAL:C

#SET SHADAPLSP:D(%APL)  = %SP
#DO  SHADCVSTAT:C
#SET SHADSTATE:D(%APL)  = %NEW_STATE
#IF %AS == 1 #THEN #BLOCK
; ****** CHECK ***********
   @GO_HOT = TRUE
   #ERROR CONTINUE
   #DO SHADGOHOT:C
   #ERROR STOP
   #IF %GO_HOT #THEN #BLOCK
      @STATUS = STATUS
      #ERROR CONTINUE
      #SET APL'APL':BAS="HOT"
      #ERROR STOP
      @STATUS = STATUS
      #IF %STATUS == 0 #THEN #BLOCK
         #EXEC SHADHOT:C
         #EXEC SHADMAPNET:C (@APL = %APL)
         #EXEC SHADMAPMON:C (@APL = %APL)
         #EXEC SHADUSR:C (@APL = %APL, @EVENT = %TOVR)
         @OS = 10
         #DO SHADEXTAS:C
         #SET SHAD_XMT:D(%APL) = 12
         #SET SHAD_XMT:DRT(%APL) = CLOCK
         #EXEC SHADLOG:C (@APL=%APL,@RT=SYS_TIME,@TEXT="TAKEOVER SUCCESSFULL")
      #BLOCK_END         ; IF STATUS == 0
      #ELSE #BLOCK
         #EXEC SHADLOG:C (@APL=%APL,@RT=SYS_TIME,-
                          @TEXT="TAKEOVER FAILED, STATUS='STATUS'")
      #BLOCK_END
   #BLOCK_END         ; IF %GO_HOT
   #ELSE #BLOCK
      #SET APL'APL':BSS = "NONE"
      #SET APL'APL':BAS = "COLD"
      #DO SHADRCVINI:C
      #SET APL'APL':BSS = "RECEIVE"
      #EXEC SHADLOG:C (@APL=%APL,@RT=SYS_TIME,-
                       @TEXT="TAKEOVER DENIED BY SHADGOHOT - RECEIVER RESTARTED")
   #BLOCK_END
#BLOCK_END
#ELSE #BLOCK
   #IF %SP == 0 #THEN #BLOCK  
               ; THIS BRANCH IS EXECUTED WHEN TAKEOVER FAILS DUE TO
               ; FAILURE TO PUT APL TO WARM
      #ERROR CONTINUE
      #SET APL'APL':BSS = "NONE"
      #DO SHADRCVINI:C
      #SET APL'APL':BSS = "RECEIVE"
      #EXEC SHADLOG:C (@APL=%APL,@RT=SYS_TIME,-
                       @TEXT="TAKEOVER FAILED BY RECEIVER - RECEIVER RESTARTED")
      WARNING -- TAKEOVER FAILED, STATUS = 'STATUS'
   #BLOCK_END         ; IF %SP == 0
#BLOCK_END


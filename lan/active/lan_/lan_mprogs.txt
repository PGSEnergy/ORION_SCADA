;LAN_MPROGS.TXT   1995-07-05 / TL
;REV.   8.4    1995-10-03 / TL

;       PIC_PROGS
;
;       In:     PICT_NUMBER
;               PICT_PFNUMBER    Picture function number, 0 if main picture
;
;       Out:    PICT_STATUS
;               PICT_BACKGROUND  The SCIL
;               PICT_DRAW        programs
;               PICT_START       as
;               PICT_UPDATE      text vectors
;               PICT_EXIT
;
;       Note:   Internal variables have the prefix PSX
;

@PICT_STATUS = STATUS

@PICT_BACKGROUND = VECTOR()
@PICT_DRAW       = VECTOR()
@PICT_START      = VECTOR()
@PICT_UPDATE     = VECTOR()
@PICT_EXIT       = VECTOR()

#ERROR IGNORE
@PICT_STATUS = GET_STATUS(%PICT_PFNUMBER)
#IF %PICT_STATUS == 0 #THEN #BLOCK
   #IF DATA_TYPE(%PICT_PFNUMBER) == "INTEGER" #THEN #BLOCK
      #IF %PICT_PFNUMBER < 0 #THEN -
         @PICT_STATUS = 201 ;SCIL_INTEGER_ARGUMENT_OUT_OF_RANGE
   #BLOCK_END
   #ELSE @PICT_STATUS = 202 ;SCIL_INTEGER_ARGUMENT_EXPECTED
#BLOCK_END
#IF %PICT_STATUS == 0 #THEN #BLOCK
   #IF %PICT_PFNUMBER == 0 #THEN @PSXSOURCE_ = ""
   #ELSE #BLOCK
      @PSXPFUNCTIONS_ = PSXP'PICT_NUMBER'.GET_PICTURE_FUNCTIONS
      @PICT_STATUS = STATUS
      #IF %PICT_STATUS == 0 #THEN #BLOCK
         #IF %PICT_PFNUMBER <= LENGTH(%PSXPFUNCTIONS_) #THEN #BLOCK
            @PSXPFUNCTION_ = %PSXPFUNCTIONS_(%PICT_PFNUMBER)
            @PSXSOURCE_ = PSXPFUNCTION_:VNAME
         #BLOCK_END
         #ELSE @PICT_STATUS = 201 ;SCIL_INTEGER_ARGUMENT_OUT_OF_RANGE
      #BLOCK_END
   #BLOCK_END
   #IF %PICT_STATUS == 0 #THEN #BLOCK
      @PICT_BACKGROUND = PSXP'PICT_NUMBER'.GET_BACKGROUND_PROGRAM(%PSXSOURCE_)
      @PICT_STATUS = STATUS
   #BLOCK_END
   #IF %PICT_STATUS == 0 #THEN #BLOCK
      @PICT_DRAW = PSXP'PICT_NUMBER'.GET_DRAW_PROGRAM(%PSXSOURCE_)
      @PICT_STATUS = STATUS
   #BLOCK_END
   #IF %PICT_STATUS == 0 #THEN #BLOCK
      @PICT_START = PSXP'PICT_NUMBER'.GET_START_PROGRAM(%PSXSOURCE_)
      @PICT_STATUS = STATUS
   #BLOCK_END
   #IF %PICT_STATUS == 0 #THEN #BLOCK
      @PICT_UPDATE = PSXP'PICT_NUMBER'.GET_UPDATE_PROGRAM(%PSXSOURCE_)
      @PICT_STATUS = STATUS
   #BLOCK_END
   #IF %PICT_STATUS == 0 #THEN #BLOCK
      @PICT_EXIT = PSXP'PICT_NUMBER'.GET_EXIT_PROGRAM(%PSXSOURCE_)
      @PICT_STATUS = STATUS
   #BLOCK_END
#BLOCK_END

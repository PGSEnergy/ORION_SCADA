@REX_FILE__ = ARGUMENT(1) ; REx configuration file name
@REX_NETNR__ = ARGUMENT(2) ; REx NET Number

@REX_CONF_FILE__ = READ_TEXT(%REX_FILE__)
@REX_ID__ = "RExCONF"
@REX_CURR_LINE__ = SELECT(%REX_CONF_FILE__, "==""['REX_ID__']""") ; General description
#IF LENGTH(%REX_CURR_LINE__) > 0 #THEN #BLOCK
 @REX_GENERAL__ = SEPARATE(%REX_CONF_FILE__(%REX_CURR_LINE__(1) + 1), ",")
 @REX_NRDEVICES__ = DEC_SCAN(%REX_GENERAL__(2))
 @REX_NRPOINTS__ = DEC_SCAN(%REX_GENERAL__(3))
 @REX_CURR_POINT__ = SELECT(%REX_CONF_FILE__, "==""[DevicePoints]""")
 #IF %REX_NRPOINTS__ > 0 #THEN @REX_SPAS__ = %REX_CONF_FILE__(%REX_CURR_POINT__(1) + 1..%REX_CURR_POINT__(1) + %REX_NRPOINTS__)
 @REX_CURR_NUMBER__ = SELECT(%REX_CONF_FILE__, "==""[DeviceNumber]""")
 @REX_DEVICES__ = %REX_CONF_FILE__(%REX_CURR_NUMBER__(1) + 1..%REX_CURR_NUMBER__(1) + %REX_NRDEVICES__)
 @REX_INDICES__ = %REX_CONF_FILE__(%REX_CURR_LINE__(1) + 2..%REX_CURR_NUMBER__(1) - 1)
 @REX_CONF_FILE__
 @REX_CURR_NUMBER__
 @REX_CURR_POINT__

 @REX_PATTRS__ = VECTOR()
 @REX_DATTRS__ = VECTOR()

 #LOOP_WITH REX_II__ = 1 .. LENGTH(%REX_INDICES__)
   @REX_CURR_LINE__ = %REX_INDICES__(%REX_II__)
   @REX_SIGN__ = LOCATE(%REX_CURR_LINE__, "=")
   @REX_UNDER__ = LOCATE(%REX_CURR_LINE__, "_")
   @REX_VARIABLE__ = SUBSTR(%REX_CURR_LINE__, %REX_UNDER__ + 1, %REX_SIGN__ - %REX_UNDER__ - 2)
   #IF (%REX_SIGN__ - %REX_UNDER__) > 4 #THEN @REX_PATTRS__(LENGTH(%REX_PATTRS__) + 1) = %REX_VARIABLE__
     #ELSE @REX_DATTRS__(LENGTH(%REX_DATTRS__) + 1) = %REX_VARIABLE__
   @REX_VALUE__ = SUBSTR(%REX_CURR_LINE__, %REX_SIGN__ + 2, 0)
   @REX_'REX_VARIABLE__'__ = 'REX_VALUE__'
 #LOOP_END

 @REX_COMATTRS__ = VECTOR()
 @REX_SPASINUSE__ = VECTOR()
 @REX_NETLINES__ = VECTOR()
 @REX_RESERVEDLINES__ = VECTOR()
 @REX_POINTS__ = VECTOR()
 @REX_CURRENT__ = VECTOR()

 #LOOP_WITH REX_II__ = 1 .. LENGTH(%REX_DEVICES__)
   @REX_CURR_LINE__ = SEPARATE(%REX_DEVICES__(%REX_II__), ",")
   @REX_DEVICE__ = %REX_CURR_LINE__(%REX_NR__) ; Device number index
   @REX_LINENR__ = %REX_CURR_LINE__(%REX_RX__) ; Line number index
   @REX_DEVICE_TN__ = STA'REX_DEVICE__':BTN
   @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1) = "#SET NET'REX_NETNR__':SRX'REX_DEVICE_TN__'='REX_LINENR__'"

   #LOOP_WITH REX_JJ__ = 1 .. LENGTH(%REX_DATTRS__)
     @REX_COMATTR__ = %REX_DATTRS__(%REX_JJ__)
     @REX_COMVALUE__ = %REX_CURR_LINE__(%REX_'REX_COMATTR__'__)
     #IF %REX_COMATTR__ == "IU" AND %REX_COMVALUE__ == "1" #THEN @REX_SPASINUSE__(LENGTH(%REX_SPASINUSE__) + 1) = "#SET STA'REX_DEVICE__':SIU=1"
     #ELSE_IF %REX_COMATTR__ == "RX" AND %REX_COMVALUE__ <> "" #THEN #BLOCK
       @REX_ALLREADYRESERVED__ = SELECT(%REX_RESERVEDLINES__, "==%REX_COMVALUE__") 
       #IF LENGTH(%REX_ALLREADYRESERVED__) == 0 #THEN #BLOCK
         @REX_RESERVEDLINES__ = APPEND(%REX_RESERVEDLINES__, %REX_COMVALUE__)
         @REX_NETLINES__(LENGTH(%REX_NETLINES__) + 1) = "#SET NET'REX_NETNR__':SIU'REX_COMVALUE__'=1"
       #BLOCK_END     
     #BLOCK_END
     #ELSE_IF %REX_COMVALUE__ <> "" AND %REX_COMATTR__ <> "NR" #THEN #BLOCK
       @REX_COMMAND__ = "#SET STA'REX_DEVICE__':S'REX_COMATTR__'='REX_COMVALUE__'"
       @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1) = %REX_COMMAND__
     #BLOCK_END
   #LOOP_END

   #IF %REX_NRPOINTS__ > 0 #THEN #BLOCK
     @REX_PP__ = 1
     #LOOP_WITH REX_KK__ = 1 .. LENGTH(%REX_SPAS__)
       @REX_CURR_LINE__ = SEPARATE(%REX_SPAS__(%REX_KK__), ",")
       #IF %REX_DEVICE__ == %REX_CURR_LINE__(%REX_NUMBER__) #THEN #BLOCK ; if relation exists
         #LOOP_WITH REX_LL__ = 1 .. LENGTH(%REX_PATTRS__)
           @REX_SELPOINT__ = %REX_PATTRS__(%REX_LL__)
           #IF %REX_SELPOINT__ == "CATEGORY" #THEN #BLOCK
             @REX_DATATYPE__ = %REX_CURR_LINE__(%REX_CATEGORY__)
             @REX_POINTS__(%REX_LL__ - 1) = """'REX_DATATYPE__'"""
           #BLOCK_END
             #ELSE_IF %REX_SELPOINT__ <> "NUMBER" #THEN @REX_POINTS__(%REX_LL__ - 1) = %REX_CURR_LINE__(%REX_'REX_SELPOINT__'__)
         #LOOP_END
         @REX_CURRENT__(LENGTH(%REX_CURRENT__) + 1) = %REX_POINTS__
         @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1) = "#SET STA'REX_DEVICE__':SSP'REX_PP__'=(" + COLLECT(%REX_POINTS__, ",") + ")"
         @REX_PP__ = %REX_PP__ + 1
       #BLOCK_END
     #LOOP_END
   #BLOCK_END
   @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1) = ""
 #LOOP_END

 @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1..) = %REX_SPASINUSE__
 @REX_COMATTRS__(LENGTH(%REX_COMATTRS__) + 1..) = %REX_NETLINES__
#BLOCK_END
#ELSE @REX_COMATTRS__ = ("ERROR: The file is not a valid REx NET Configuration File.", "")

#DO %REX_COMATTRS__

; @REX_COMATTRS__